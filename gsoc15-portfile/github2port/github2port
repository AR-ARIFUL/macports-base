#!/usr/bin/env tclsh
# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
#
# Generate a github group template Portfile given tarball URL
#
# https://github.com/tmux/tmux/releases/download/2.0/tmux-2.0.tar.gz
# https://github.com/JuliaLang/julia/releases/download/v0.3.8/julia-0.3.8_79599ada44.tar.gz
# https://github.com/ali-rantakari/trash/archive/v0.8.5.tar.gz

# TODO: Create a usage proc
if {$argc != 1} {
    puts stderr "Usage: $argv0 URL"
    puts stderr ""
    puts stderr "URL is the github tarball URL"
    puts stderr ""
    puts stderr "Example: $argv0 https://github.com/tmux/tmux/releases/download/2.0/tmux-2.0.tar.gz"
    puts stderr ""
    exit 1
}

set url [lindex $argv 0]

set author author
set project project
set version 1.0
set tag_prefix "";              # tag_prefix, usually is empty or "v"

set tarball_from "";            # tarball type, including release and others

# 2.0, v0.3.8, release-v1.5.8, v0.8.5.tar.gz => version and tag_prefix if has
proc parse_fancy_version {v} {
    global version
    global tag_prefix
    if {[regexp {[0-9]+([.-][0-9]+)+} $v version]} {
        if {[string length $version] < [string length $v]} {
            set tag_prefix [string range $v 0 [expr [string first $version $v] -1]]
        }
    }
}

if {[string match */releases/download/* $url]} {
    set tarball_from releases

    set tmp [split $url /]
    set author [lindex $tmp 3]
    set project [lindex $tmp 4]
    # 2.0, v0.3.0
    set github_version [lindex $tmp 7]
    parse_fancy_version $github_version
} elseif {[string match */archive/* $url]} {
    set tarball_from ""

    set tmp [split $url /]
    set author [lindex $tmp 3]
    set project [lindex $tmp 4]
    # v0.8.5.tar.gz
    set tarball [lindex $tmp 6]
    parse_fancy_version $tarball
}

puts "# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4"
puts "# \$Id\$"
puts ""
puts "PortSystem          1.0"

puts "PortGroup           github 1.0"
puts ""
puts "github.setup        ${author} ${project} ${version} ${tag_prefix}"

# TODO: 6. (optional) Query categories, license etc from other Package Manager
puts "categories          replaceme"
puts "platforms           darwin"

# TODO: 5. (optional) Support environment variable like MaintainerHandle, use USER as fallback
puts "maintainers         replaceme"
puts "license             replaceme"

# TODO: 4. (optional) Get description using github API
puts ""
puts "description         replaceme"
puts "long_description    replaceme"
puts ""

if {![string equal $tarball_from ""]} {
    puts "github.tarball_from releases"
}

puts ""
puts "checksums           rmd160  12345 \\"
puts "                    sha256  6789a"
